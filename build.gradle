plugins {
    id 'java'
    id 'org.springframework.boot' version '3.3.1'
    id 'io.spring.dependency-management' version '1.1.5'
    id 'idea'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'java-library'
}

idea {
    project {
        languageLevel = 17
    }
    module {
        downloadJavadoc = true
        downloadSources = true
    }
}

group = 'com.playhub.game.initializer'
version = '0.0.1-SNAPSHOT'

ext {
    bomVersions = [
            springBoot: "3.2.5",
            springCloud: "2022.0.3",
            mongock: "5.4.4"
    ]
    libVersions = [
        mapstruct: "1.6.0",
        playhubSecurityStarter: "0.0.1-SNAPSHOT",
        jimBeamCommonUtils: "0.0.1-SNAPSHOT",
        jimBeamJpaUtils: "0.0.1-SNAPSHOT",
        jimBeamPostgresContainer: "0.0.1-SNAPSHOT",
        jimBeamKafkaContainer: "0.0.1-SNAPSHOT",
        jimBemSpringLiquibase: "0.0.1-SNAPSHOT",
        playhubWebErrorManagementStarter: "0.0.1-SNAPSHOT",
        springDocOpenApi: "2.0.2",
        mongock: "5.4.4"
    ]
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

configurations {
    compileOnly {
        extendsFrom annotationProcessor
    }
}

bootJar {
    mainClass.set("module service.com.playhub.gameinitializer.Application")
}

repositories {
    mavenCentral()
    mavenLocal()
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'io.spring.dependency-management'
    apply plugin: 'idea'

    repositories {
        mavenCentral()
        mavenLocal()
        maven {
            name = "Test-Utils"
            url = uri("https://maven.pkg.github.com/dalayLama/test-utils")
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
        maven {
            name = "Playhub-Common-Libs"
            url = uri("https://maven.pkg.github.com/dalayLama/playhub-common-libs")
            credentials {
                username = project.findProperty("gpr.user")
                password = project.findProperty("gpr.key")
            }
        }
    }

    dependencyManagement {
        dependencies {
            imports {
                mavenBom "org.springframework.boot:spring-boot-dependencies:$bomVersions.springBoot"
                mavenBom "org.springframework.cloud:spring-cloud-dependencies:$bomVersions.springCloud"
                mavenBom "io.mongock:mongock-bom:$bomVersions.mongock"
            }
            dependency "org.mapstruct:mapstruct:$libVersions.mapstruct"
            dependency "org.mapstruct:mapstruct-processor:$libVersions.mapstruct"
            dependency "com.playhub.common:playhub-web-error-management-starter:$libVersions.playhubWebErrorManagementStarter"
            dependency "com.playhub.common:playhub-rest-security-starter:$libVersions.playhubSecurityStarter"
            dependency "com.jimbeam:common-utils:$libVersions.jimBeamCommonUtils"
            dependency "com.jimbeam:spring-jpa-utils:$libVersions.jimBeamJpaUtils"
            dependency "com.jimbeam:postgres-testcontainer:$libVersions.jimBeamPostgresContainer"
            dependency "com.jimbeam:kafka-testcontainer:$libVersions.jimBeamKafkaContainer"
            dependency "com.jimbeam:spring-liquibase:$libVersions.jimBemSpringLiquibase"
            dependency "org.springdoc:springdoc-openapi-starter-webmvc-ui:$libVersions.springDocOpenApi"
            dependency "io.mongock:mongock-springboot-v3:$libVersions.mongock"
            dependency "io.mongock:mongodb-springdata-v4-driver:$libVersions.mongock"
        }
    }

    dependencies {
        annotationProcessor 'org.projectlombok:lombok'
        compileOnly 'org.projectlombok:lombok'

        testAnnotationProcessor 'org.projectlombok:lombok'
        testCompileOnly 'org.projectlombok:lombok'
        testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
        testImplementation 'org.springframework.boot:spring-boot-starter-test'
    }

    tasks.withType(JavaCompile) {
        options.compilerArgs << "-parameters"
        options.compilerArgs << "-Amapstruct.disableBuilders=true"
    }

    tasks.named('test') {
        useJUnitPlatform()
    }

}
